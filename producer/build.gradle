plugins {
	id 'java'
	id 'org.springframework.boot' version '3.0.5'
	id 'io.spring.dependency-management' version '1.1.0'
	id 'com.github.davidmc24.gradle.plugin.avro-base'  version '1.0.0'
	id 'idea'
}

group = 'com.example'
version = '0.0.1-SNAPSHOT'
sourceCompatibility = '17'

configurations {
	compileOnly {
		extendsFrom annotationProcessor
	}
}

repositories {
	mavenCentral()
	maven {
		url "https://packages.confluent.io/maven/"
	}
}

//Intellij wasn't seeing generated java
def generatedSourcesDir = file('src/generated/main/java')
idea {
	module {
		sourceDirs += generatedSourcesDir
		generatedSourceDirs += generatedSourcesDir
	}
}

dependencies {
	implementation 'org.springframework.boot:spring-boot-starter-web'
	implementation 'org.springframework.kafka:spring-kafka'
	compileOnly 'org.projectlombok:lombok'
	annotationProcessor 'org.projectlombok:lombok'
	testImplementation 'org.springframework.boot:spring-boot-starter-test'
	testImplementation 'org.springframework.kafka:spring-kafka-test'

	implementation "org.apache.avro:avro:1.11.0"
	implementation 'io.confluent:kafka-avro-serializer:7.3.3'
}

def generateAvro = tasks.register("generateAvro", com.github.davidmc24.gradle.plugin.avro.GenerateAvroJavaTask) {
	source("src/main/avro")
	outputDir = file("src/main/java")
	fieldVisibility = "PRIVATE"
	enableDecimalLogicalType = true
}

clean {
	delete "${rootDir}/src/generated/main/java"
}

tasks.named('test') {
	useJUnitPlatform()
}

tasks.withType(JavaCompile).configureEach {
	// Make Java compilation tasks depend on the Avro code generation task.
	dependsOn('generateAvro')
}